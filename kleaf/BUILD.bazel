# Copyright (C) 2021 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@io_bazel_stardoc//stardoc:stardoc.bzl", "stardoc")
load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag", "string_flag")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("//build/bazel_common_rules/dist:dist.bzl", "copy_to_dist_dir")

package(default_visibility = ["//visibility:public"])

# Build flags

string_flag(
    name = "lto",
    build_setting_default = "default",
    values = [
        "default",
        "none",
        "thin",
        "full",
    ],
)

# Debug options

bool_flag(
    name = "debug_annotate_scripts",
    build_setting_default = False,
)

bool_flag(
    name = "debug_print_scripts",
    build_setting_default = False,
)

exports_files([
    "preserve_env.sh",
    "search_and_mv_output.py",
    "check_duplicated_files_in_archives.py",
])

# Docs. To view them:
#   bazel build //build/kleaf:docs
#   # Examine bazel-bin/build/kleaf/*.md

kernel_rule_files = [
    "common_kernels",
    "kernel",
]

bzl_library(
    name = "kernel-rules",
    srcs = [name + ".bzl" for name in kernel_rule_files] + [
        "@bazel_skylib//rules:common_settings",
        "//build/bazel_common_rules/dist:dist",
    ],
)

[stardoc(
    name = name + "-docs",
    out = name + ".md",
    func_template = "templates/func.vm",
    input = name + ".bzl",
    provider_template = "templates/provider.vm",
    rule_template = "templates/rule.vm",
    deps = ["kernel-rules"],
) for name in kernel_rule_files]

filegroup(
    name = "docs",
    srcs = [name + "-docs" for name in kernel_rule_files],
)

#
# Remote Build Execution (RBE) Definitions
#
_CONTAINER = "gcr.io/androidbuild-re-dockerimage/android-build-remoteexec-image"

_CONTAINER_VERSION = "sha256:582efb38f0c229ea39952fff9e132ccbe183e14869b39888010dacf56b360d62"

platform(
    name = "rbe_platform",
    constraint_values = [
        "@bazel_tools//platforms:linux",
        "@bazel_tools//platforms:x86_64",
    ],
    exec_properties = {
        "container-image": "docker://%s@%s" % (_CONTAINER, _CONTAINER_VERSION),
        "OSFamily": "Linux",
    },
    parents = ["@local_config_platform//:host"],
)

#
# Documentation
#
docs_server_files = [
    "index.html",
]

doc_root = "docs_server_files/"

[copy_file(
    name = "docs_server_files_" + file,
    src = "docs/" + file,
    out = doc_root + file,
) for file in docs_server_files]

genrule(
    name = "docs_server_files_directory.html.frag",
    srcs = [
    ],
    outs = [
        doc_root + "directory.html.frag",
    ],
    cmd = "".join([
        """echo '<div><a href="?file={name}.md">{name}.md</a></div>' >> $@ && """.format(name = name)
        for name in kernel_rule_files
    ]) + ":",
)

[copy_file(
    name = "docs_server_files_" + name + ".md",
    src = name + ".md",
    out = doc_root + name + ".md",
) for name in kernel_rule_files]

filegroup(
    name = "docs_files",
    srcs =
        [doc_root + "directory.html.frag"] +
        ["docs_server_files_" + name + ".md" for name in kernel_rule_files] +
        ["docs_server_files_" + name for name in docs_server_files],
)

sh_binary(
    name = "docs_server",
    srcs = [
        "docs/run_server.sh",
    ],
    data = [":docs_files"],
)

copy_to_dist_dir(
    name = "docs_dist",
    data = [":docs_files"],
)
