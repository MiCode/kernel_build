# Copyright (C) 2021 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag", "string_flag")
load("//build/bazel_common_rules/docs:docs.bzl", "docs")
load("//build/kernel/kleaf/tests:empty_test.bzl", "empty_test")
load("//build/kernel/kleaf/impl:ddk/gen_ddk_headers.bzl", "gen_ddk_headers")
load(":constants.bzl", "LTO_VALUES")
load(":print_flags.bzl", "print_flags")

package(default_visibility = ["//visibility:public"])

# Build flags

string_flag(
    name = "lto",
    build_setting_default = "default",
    values = LTO_VALUES,
)

config_setting(
    name = "lto_is_default",
    flag_values = {"//build/kernel/kleaf:lto": "default"},
)

# If --lto=none, or if --kasan, LTO should be set to none.
selects.config_setting_group(
    name = "lto_is_none",
    match_any = [
        "//build/kernel/kleaf/impl:lto_is_set_to_none",
        "//build/kernel/kleaf/impl:lto_is_default_and_kasan_is_true",
    ],
)

config_setting(
    name = "lto_is_thin",
    flag_values = {":lto": "thin"},
)

config_setting(
    name = "lto_is_full",
    flag_values = {":lto": "full"},
)

config_setting(
    name = "lto_is_fast",
    flag_values = {":lto": "fast"},
)

# If true, kernel_images sets EXT_MODULES before creating the staging directory.
# Note: This is a temporary flag to mitigate issues on migrating away from
# setting EXT_MODULES in build.config. If you need to disable this flag, please
# file a bug.
bool_flag(
    name = "set_ext_modules",
    build_setting_default = True,
)

string_flag(
    name = "cache_dir",
    build_setting_default = "",
)

# If true, set `KBUILD_SYMTYPES=1` for kernel_build() with
# kbuild_symtypes="auto".
bool_flag(
    name = "kbuild_symtypes",
    build_setting_default = False,
)

# If true, enable kasan. This assumes --lto=none, but :print_flags only shows
# values from the command line.
bool_flag(
    name = "kasan",
    build_setting_default = False,
)

config_setting(
    name = "kasan_is_true",
    flag_values = {":kasan": "1"},
)

# If true, //{common}:all_headers also includes //{common}:all_headers_unsafe.
bool_flag(
    name = "allow_ddk_unsafe_headers",
    build_setting_default = False,
)

# If true, *.gcno files are collected.
bool_flag(
    name = "gcov",
    build_setting_default = False,
)

# If true, allow modules to be undeclared in kernel_build.
bool_flag(
    name = "allow_undeclared_modules",
    build_setting_default = False,
)

# If true, warnings / errors about undeclared modules in kernel_build are printed.
bool_flag(
    name = "warn_undeclared_modules",
    build_setting_default = True,
)

# If true, everything inside OUT_DIR is collected unconditionally
bool_flag(
    name = "preserve_kbuild_output",
    build_setting_default = False,
)

config_setting(
    name = "allow_ddk_unsafe_headers_set",
    flag_values = {
        ":allow_ddk_unsafe_headers": "1",
    },
)

# If true, kmi_symbol_list_strict_mode attribute in kernel_build is respected.
bool_flag(
    name = "kmi_symbol_list_strict_mode",
    build_setting_default = True,
)

# If false, kmi_symbol_list_violations_check is disabled.
bool_flag(
    name = "kmi_symbol_list_violations_check",
    build_setting_default = True,
)

# Flag to enable/disable BTF debug information
string_flag(
    name = "btf_debug_info",
    build_setting_default = "default",
    values = [
        "default",  # don't change generation of BTF debug information
        "enable",
        "disable",
    ],
)

# Debug options

bool_flag(
    name = "debug_annotate_scripts",
    build_setting_default = False,
)

bool_flag(
    name = "debug_print_scripts",
    build_setting_default = False,
)

# If true, set KBUILD_MODPOST_WARN=1.
# https://www.kernel.org/doc/html/latest/kbuild/kbuild.html#kbuild-modpost-warn
bool_flag(
    name = "debug_modpost_warn",
    build_setting_default = False,
)

# Debug flag to control make verbosity
# E (default) = Error (-s)
# I = Info
# D = Debug (V=1)
# V = Verbose (V=2)
string_flag(
    name = "debug_make_verbosity",
    build_setting_default = "E",
    values = [
        "V",
        "D",
        "E",
        "I",
    ],
)

# If true, enable CONFIG_GDB_SCRIPTS and collect gdb scripts.
# If additional hacks are needed, put `select()` on the build_config.
# See kleaf/docs/kgdb.md for details.
bool_flag(
    name = "kgdb",
    build_setting_default = False,
)

config_setting(
    name = "kgdb_is_true",
    flag_values = {":kgdb": "1"},
)

# The values of --config, as passed by command line and bazelrc files.
_config_values = (
    "local",
    "fast",
    "hermetic_cc",
    "stamp",
    "release",
    "remote",
)

[bool_flag(
    name = "config_" + value,
    build_setting_default = False,
) for value in _config_values]

print_flags(
    name = "print_flags",
    flags = [
        ":kasan",
        ":kbuild_symtypes",
        ":lto",
    ] + [":config_" + value for value in _config_values],
)

# Tools visible to all packages that uses kernel_build.
# Implementation detail of kernel_build; do not use directly.
sh_binary(
    name = "preserve_env",
    srcs = ["preserve_env.sh"],
    visibility = ["//visibility:public"],
)

# Tools visible to all packages that uses kernel_build etc.
# Implementation detail of kernel_build; do not use directly.
py_binary(
    name = "check_declared_output_list",
    srcs = ["check_declared_output_list.py"],
    visibility = ["//visibility:public"],
)

# Tools visible to all packages that uses kernel_modules_install.
# Implementation detail of kernel_build; do not use directly.
py_binary(
    name = "check_duplicated_files_in_archives",
    srcs = ["check_duplicated_files_in_archives.py"],
    visibility = ["//visibility:public"],
)

# Tools visible to all packages that uses various Kleaf build rules.
# Implementation detail of kernel_build; do not use directly.
py_binary(
    name = "search_and_cp_output",
    srcs = ["search_and_cp_output.py"],
    visibility = ["//visibility:public"],
)

# Analyze DDK targets

# Usage:
#  # Analyze headers used by all external modules of a device:
#  bazel run //build/kernel/kleaf:gen_ddk_headers \
#    --gen_ddk_headers_target=//label/of:tuna_modules_install \
#    --gen_ddk_headers_gen_files_archives=//common:kernel_aarch64_ddk_allowlist_headers \
#    -- [flags]
#
#  # Analyze headers used by a specific module of a device:
#  bazel run //build/kernel/kleaf:gen_ddk_headers \
#    --gen_ddk_headers_target=//label/of:tuna_ext_module \
#    --gen_ddk_headers_gen_files_archives=//common:kernel_aarch64_ddk_allowlist_headers \
#    -- [flags]
#
#  # Analyze headers used by in-tree modules of a device:
#  bazel run //build/kernel/kleaf:gen_ddk_headers \
#    --gen_ddk_headers_target=//label/of:tuna \
#    --gen_ddk_headers_gen_files_archives=//common:kernel_aarch64_ddk_allowlist_headers \
#    -- [flags]
gen_ddk_headers(
    name = "gen_ddk_headers",
    gen_files_archives = [":gen_ddk_headers_gen_files_archives"],
    target = ":gen_ddk_headers_target",
)

label_flag(
    name = "gen_ddk_headers_target",
    build_setting_default = ":gen_ddk_headers_target_unspecified",
)

label_flag(
    name = "gen_ddk_headers_gen_files_archives",
    build_setting_default = ":gen_ddk_headers_target_unspecified",
)

filegroup(
    name = "gen_ddk_headers_target_unspecified",
    srcs = [],
    visibility = ["//visibility:private"],
)

py_library(
    name = "buildozer_command_builder",
    srcs = ["buildozer_command_builder.py"],
    srcs_version = "PY3",
    visibility = ["//build/kernel/kleaf:__subpackages__"],
)

bzl_library(
    name = "directory_with_structure_def",
    srcs = ["directory_with_structure.bzl"],
    visibility = ["//build/kernel/kleaf:__subpackages__"],
    deps = ["@bazel_skylib//lib:paths"],
)

bzl_library(
    name = "hermetic_tools_def",
    srcs = ["hermetic_tools.bzl"],
    visibility = ["//build/kernel/kleaf:__subpackages__"],
    deps = ["@bazel_skylib//lib:paths"],
)

bzl_library(
    name = "update_source_file_def",
    srcs = ["update_source_file.bzl"],
    visibility = ["//build/kernel/kleaf:__subpackages__"],
    deps = [":hermetic_tools_def"],
)

# Docs. To view them:
#   bazel run //build/kernel/kleaf:docs_server

docs(
    name = "docs",
    srcs = [
        "common_kernels.bzl",
        "constants.bzl",
        "directory_with_structure.bzl",
        "download_repo.bzl",
        "fail.bzl",
        "hermetic_tools.bzl",
        "kernel.bzl",
        "key_value_repo.bzl",
        "print_debug.bzl",
        "update_source_file.bzl",
        "workspace.bzl",
        "workspace_epilog.bzl",
    ],
    default = "kernel.bzl",
    deps = [
        "//build/bazel_common_rules/dist",
        "//build/bazel_common_rules/exec",
        "//build/bazel_common_rules/workspace",
        "//build/kernel/kleaf/artifact_tests",
        "//build/kernel/kleaf/impl",
        "@bazel_skylib//:workspace",
        "@bazel_skylib//lib:dicts",
        "@bazel_skylib//lib:paths",
        "@bazel_skylib//lib:unittest",
        "@bazel_skylib//rules:common_settings",
        "@kernel_toolchain_info//:dict",
    ],
)

#
# Remote Build Execution (RBE) Definitions
#
_CONTAINER = "gcr.io/androidbuild-re-dockerimage/android-build-remoteexec-image"

_CONTAINER_VERSION = "sha256:582efb38f0c229ea39952fff9e132ccbe183e14869b39888010dacf56b360d62"

platform(
    name = "rbe_platform",
    constraint_values = [
        "@bazel_tools//platforms:linux",
        "@bazel_tools//platforms:x86_64",
    ],
    exec_properties = {
        "container-image": "docker://%s@%s" % (_CONTAINER, _CONTAINER_VERSION),
        "OSFamily": "Linux",
    },
    parents = ["@local_config_platform//:host"],
)

# tests

empty_test(
    name = "empty_test",
)

py_test(
    name = "check_declared_output_list_test",
    srcs = ["check_declared_output_list_test.py"],
    python_version = "PY3",
    deps = [
        ":check_declared_output_list",
        "@io_abseil_py//absl/testing:absltest",
    ],
)

# Quick test on Kleaf static definitions and scripts, but not built artifacts
test_suite(
    name = "quick_tests",
    tests = [
        ":check_declared_output_list_test",
        ":empty_test",
        "//build/bazel_common_rules/exec/tests",
        "//build/kernel/kleaf/tests",
        "//prebuilts/build-tools/tests",
    ],
)
